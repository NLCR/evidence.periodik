plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.6'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'cz.incad.nkp.inprove'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    lombok = '1.18.38'
    openapi = "2.8.8"
    jackson = '2.19.0'
    solrj = '9.7.0'
    mapstruct = '1.6.3'
    slf4j = '2.0.17'
    logback = '1.5.18'
    hibernate = '6.6.4.Final'
}

repositories {
    mavenCentral()
}

bootJar {
    archiveFileName.set("permonik-api.jar")
    layered {
        enabled = true
    }
    manifest {
        attributes(
                'Implementation-Title': 'Permonik BE',
                'Implementation-Version': archiveVersion
        )
    }
}

jar {
    // disabled generating *-plain.jar version during build
    // https://stackoverflow.com/questions/67663728/spring-boot-2-5-0-generates-plain-jar-file-can-i-remove-it
    enabled = false
}

dependencies {
    // SPRING BOOT
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'jakarta.ws.rs:jakarta.ws.rs-api:4.0.0'

    // HIBERNATE (preCreate, preUpdate, preRemove)
    implementation "org.hibernate.orm:hibernate-envers:${hibernate}"

    // SOLR
    implementation "org.apache.solr:solr-solrj:${solrj}"

    // JACKSON
    implementation "com.fasterxml.jackson.core:jackson-core:${jackson}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jackson}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jackson}"

    // LOMBOK
    compileOnly "org.projectlombok:lombok:${lombok}"
    annotationProcessor "org.projectlombok:lombok:${lombok}"

    // MAPSTRUCT
    implementation "org.mapstruct:mapstruct:${mapstruct}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstruct}"

    // SWAGGER / OpenAPI
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${openapi}"

    // LOGGING
    implementation "ch.qos.logback:logback-classic:${logback}"
    implementation "org.slf4j:slf4j-api:${slf4j}"

    // TESTING
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}


tasks.named('test') {
    useJUnitPlatform()
}
